/* rexx */
/*%COPYRIGHT (c) 2019 plape03 and vlcvi01. All rights reserved.       */
/*----------------------------------------------------------------------
 * Name: SYSVIEW-CLI V 0.1 
 *
 * Author: plape03 and vlcvi01
 *
 * Function: This REXX starts sysview rexx api from a uss prompt
 * Installation: 
 * 1) upload sysview-cli to a uss folder
 * 2) chmod +x sysview-cli
 *--------------------------------------------------------------------*/

arg ptgrexshStatement 
if ARG() = 0 then do
    say 'sysview-cli executes a Sysview command like JOBSUM, ...'
    say 'version 0.1'
    say ''
    say ''
    say 'Examples'
    say '========'
    say 'sysview-cli <SYSVIEW COMMAND>'
    say 'sysview-cli JOBSUM'
    say 'sysview-cli "PRE VLCVI*;JOBSUM; SORT JOBNAME DESC"'
    say ''
    say ' - nice scrolling can be achieved via less -S'
    say 'sysview-cli "ASPERF; SELECT JOBNAME = ZOWE*" | less -S'
    say ''
    say ' - remote execution'
    say 'ssh your_mf_host ''/path/sysview-cli "asperf ; SELECT JOBNAME = M*"'''
    say ''
    say ' - override default Sysview loadlib via STEPLIB'
    say 'STEPLIB=MY.CUSTOM.CNM4BLOD sysview-cli JOBSUM'
    say ''
    say ' - integration with other *nix tools like tail, awk'
    say '   field separator is |'
    say 'sysview-cli "ASPERF; SELECT JOBNAME = ZOWE*" | tail +8 | awk -F ''|'' ''{print $4}'''
    say 'sysview-cli "PRE ZOWE*;JOBSUM" | tail +8 | awk -F ''|'' ''{print $3}'''

    exit 12
end

ptgrexshPrompt = ">>>"
ptgrexshError = "!!!"
call on halt name PTGREXSH_halt
signal on failure name PTGREXSH_errorHandler
signal on syntax name PTGREXSH_errorHandler
PTGREXSH_errorRc = 0
call SYSVIEWE "*" "C(PROFILE SWITCH DEFAULT;SET APIDLMCHAR |;PREFIX;OWNER;" ptgrexshStatement ") DELIMITER(|) LIMIT(100000) SIZE(100,1320)"
PTGREXSH_resume:
exit PTGREXSH_errorRc

SYSVIEWE: procedure expose GSVXAPIE_APIC ptgrexshPrompt ptgrexshError PTGREXSH_errorRc
    parse arg filter command

    address LINK "GSVXRXAA"
    if rc <> 0 then do
        say 'R'rc
        say 'MGSVXRXAA LINK failed'
        PTGREXSH_errorRc = rc
        return
    end

    address SYSVIEWE command

    PTGREXSH_errorRc = rc
    say 'R'rc
    do while queued() > 0
        parse pull val
        if filter = '*' then do
            say val
        end
        else do
            if length(val) > 0 then do
                recordType = substr(val, 1, 1)
            end
            else do
                recordType = ''
            end
            if pos(recordType, filter) > 0 then do
                say val
            end
        end
    end
return

PTGREXSH_errorHandler:
    PTGREXSH_errorRc = rc
    say ptgrexshError condition('C')':' 'RC('PTGREXSH_errorRc')'
    signal PTGREXSH_resume

PTGREXSH_halt:
    exit 0
