/* rexx */
/*
 * This program and the accompanying materials are made available under the terms of the
 * Eclipse Public License v2.0 which accompanies this distribution, and is available at
 * https://www.eclipse.org/legal/epl-v20.html
 *
 * SPDX-License-Identifier: EPL-2.0
 *
 * Copyright Contributors to the plape03 and vlcvi01.
 *
 *----------------------------------------------------------------------
 * Name: SYSVIEW-CLI V 0.1 
 *
 * Author: plape03 and vlcvi01
 *
 * Function: This REXX starts sysview rexx api from a uss prompt
 * Installation: 
 * 1) upload sysview-cli to a uss folder
 * 2) chmod +x sysview-cli
 *--------------------------------------------------------------------*/

arg sysviewrexshStatement 
if ARG() = 0 then do
    say 'sysview-cli executes a Sysview command like JOBSUM, ...'
    say 'version 0.1'
    say ''
    say ''
    say 'Examples'
    say '========'
    say 'sysview-cli <SYSVIEW COMMAND>'
    say 'sysview-cli JOBSUM'
    say 'sysview-cli "PRE VLCVI*;JOBSUM; SORT JOBNAME DESC"'
    say ''
    say ' - nice scrolling can be achieved via less -S'
    say 'sysview-cli "ASPERF; SELECT JOBNAME = ZOWE*" | less -S'
    say ''
    say ' - remote execution'
    say 'ssh your_mf_host ''/path/sysview-cli "asperf ; SELECT JOBNAME = M*"'''
    say ''
    say ' - override default Sysview loadlib via STEPLIB'
    say 'STEPLIB=MY.CUSTOM.CNM4BLOD sysview-cli JOBSUM'
    say ''
    say ' - integration with other *nix tools like tail, awk'
    say '   field separator is |'
    say 'sysview-cli "ASPERF; SELECT JOBNAME = ZOWE*" | tail +8 | awk -F ''|'' ''{print $4}'''
    say 'sysview-cli "PRE ZOWE*;JOBSUM" | tail +8 | awk -F ''|'' ''{print $3}'''

    exit 12
end

sysviewrexshPrompt = ">>>"
sysviewrexshError = "!!!"
call on halt name SYSVIEWREXSH_halt
signal on failure name SYSVIEWREXSH_errorHandler
signal on syntax name SYSVIEWREXSH_errorHandler
SYSVIEWREXSH_errorRc = 0
call SYSVIEWE "*" "C(PROFILE SWITCH DEFAULT;SET APIDLMCHAR |;PREFIX;OWNER;" sysviewrexshStatement ") DELIMITER(|) LIMIT(100000) SIZE(100,1320)"
SYSVIEWREXSH_resume:
exit SYSVIEWREXSH_errorRc

SYSVIEWE: procedure expose GSVXAPIE_APIC sysviewrexshPrompt sysviewrexshError SYSVIEWREXSH_errorRc
    parse arg filter command

    address LINK "GSVXRXAA"
    if rc <> 0 then do
        say 'R'rc
        say 'MGSVXRXAA LINK failed'
        SYSVIEWREXSH_errorRc = rc
        return
    end

    address SYSVIEWE command

    SYSVIEWREXSH_errorRc = rc
    say 'R'rc
    do while queued() > 0
        parse pull val
        if filter = '*' then do
            say val
        end
        else do
            if length(val) > 0 then do
                recordType = substr(val, 1, 1)
            end
            else do
                recordType = ''
            end
            if pos(recordType, filter) > 0 then do
                say val
            end
        end
    end
return

SYSVIEWREXSH_errorHandler:
    SYSVIEWREXSH_errorRc = rc
    say sysviewrexshError condition('C')':' 'RC('SYSVIEWREXSH_errorRc')'
    signal SYSVIEWREXSH_resume

SYSVIEWREXSH_halt:
    exit 0
